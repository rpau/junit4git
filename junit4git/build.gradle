dependencies {
    compile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.ea.agentloader:ea-agent-loader:1.0.3'
    compile "org.nanohttpd:nanohttpd:2.2.0"
    compile "com.squareup.okhttp3:okhttp:3.9.0"
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.8.0.201706111038-r'
    compile group: 'org.javassist', name: 'javassist', version: '3.22.0-GA'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

Map <String, ?> attrs = [ 'Premain-Class': 'org.walkmod.junit4git.core.ignorers.TestIgnorerAgent',
                          'Can-Retransform-Classes': true,
                          'Boot-Class-Path': 'junit4git-agent.jar'
]

task customFatJar(type: Jar) {
    manifest {
        attributes attrs
    }
    baseName = 'junit4git-agent'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it)}
    }
    exclude ('**/*.RSA')
    with jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact packageJavadoc

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'junit4git'
                    description 'A test library for incremental Junit tests execution'
                    url 'https://github.com/rpau/junit4git'
                    scm {
                        url 'https://github.com/rpau/junit4git'
                        connection 'https://github.com/rpau/junit4git.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'rpau'
                            name 'Raquel Pau'
                            email 'rpau@walkmod.com'
                        }
                    }
                }
            }
        }
    }
}
