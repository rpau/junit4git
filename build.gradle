group 'org.walkmod'
version '1.0'

buildscript {
    repositories {
        mavenCentral()
        // The following is only necessary if you want to use SNAPSHOT releases.
        // maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

dependencies {
    compile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.ea.agentloader:ea-agent-loader:1.0.3'
    compile "org.nanohttpd:nanohttpd:2.2.0"
    compile "com.squareup.okhttp3:okhttp:3.9.0"
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.8.0.201706111038-r'
    compile group: 'org.javassist', name: 'javassist', version: '3.22.0-GA'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact packageJavadoc

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'smart-testing'
                    description 'A test library for incremental Junit tests execution'
                    url 'https://github.com/rpau/junit4git'
                    scm {
                        url 'https://github.com/rpau/smart-testing'
                        connection 'https://github.com/rpau/junit4git.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'rpau'
                            name 'Raquel Pau'
                            email 'rpau@walkmod.com'
                        }
                    }
                }
            }
        }
    }
}
